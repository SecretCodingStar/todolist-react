buildscript {
	ext {
		springBootVersion = '1.3.5.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

mainClassName='cn.nonocast.TodolistApplication'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def getProjectVersion() {
    // 获取application.properties中的project.verion
    def properties = new Properties()
    def propertiesFile = new File('src/main/resources/application.properties')
    propertiesFile.withInputStream {
        properties.load(it)
    }

    return properties['project.version']
}

def projectVersion = getProjectVersion()

jar {
    baseName = 'todoapp'
    version = projectVersion
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.boot:spring-boot-starter-freemarker"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.webjars:bootstrap:3.3.6"
    compile "org.webjars:font-awesome:4.6.3"
    compile "org.webjars:jquery:2.2.4"
    compile 'org.webjars:coffee-script:1.10.0'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    testCompile "org.springframework.boot:spring-boot-starter-test"
    runtime 'mysql:mysql-connector-java'
}

// gradle -Paction=xxx dba
// default: rebuild
task dba << {
    def command = "java -cp build/libs/todoapp-${projectVersion}.jar cn.nonocast.DBA"
    print command.execute().text
}

dba.dependsOn build

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
